set antialias                     " Smooth fonts
set guioptions-=T                 " Hide toolbar
set background=light              " Light background when using GUI
set guicursor=n:blinkon0          " No blinking the cursor in normal mode
set lines=999 columns=999         " Open maximized

" save session data on exit
let sessionman_save_on_exit=1

" load last session on enter, creating a default session if necessary
function LoadSessionAndSetTitle()
  " note - sessionman plugin stores last opened session name in g:LAST_SESSION
  if argc() == 0 && ( v:servername == 'VIM' || v:servername == 'GVIM' )
    autocmd BufEnter * if &ft == '' && &buftype == '' | filetype detect
    if !filereadable($HOME . '/.vim/sessions/default')
      execute 'SessionSaveAs default'
    elseif exists('g:LAST_SESSION')
      execute 'SessionOpenLast'
    else
      execute 'SessionOpen default'
    endif
  elseif exists('g:LAST_SESSION')
    unlet g:LAST_SESSION
  endif
  " note - always check for existence of g:LAST_SESSION when setting title, it
  " may be cleared above, but will be re-created if a session is opened later.
  let &titlestring = "%f - %{v:servername} %{exists('g:LAST_SESSION')?'('.(g:LAST_SESSION).')':''}"
endfunction
autocmd VimEnter * call LoadSessionAndSetTitle()

if has("mac")

  " Toggle full-screen with Cmd+Enter
  map <D-Enter> :set invfu<CR>

  " Set the guifont
  set guifont=Mensch:h12,Menlo:h12,Monaco:h12

  " macvim container window gets screwed up after opening and
  " closing tabs, so map leader x to expand (um, kind of)
  map <leader>x :set lines=999 columns=999<Enter>
  " addition - just autocmd it
  " TODO: remove the mapping if it turns out to be unnecessary
  autocmd TabEnter * set lines=999 columns=999

  " MacVim disables cocoa key bindings, but I want double S / section sign
  " mapped to hash. For some reason map doesn't work, so...
  imap ยง #
  cmap ยง #
  nmap ยง #
  vmap ยง #

  " Workarounds for rendering glitches with quickfix windows on macvim
  " See https://code.google.com/p/macvim/issues/detail?id=409

  " Remap CTRL-L to clear screen before redrawing - fixes borked rendering
  " This may no longer be necessary, if the hacks below work as expected
  map <silent> <C-L> :nohl<CR>:redraw!<CR>

  " Clear and redraw screen when entering and exiting CtrlP buffer
  let g:ctrlp_buffer_func = {
    \ 'enter': 'RedrawScreen',
    \ 'exit':  'RedrawScreen',
    \ }
  function RedrawScreen()
    exec 'redraw!'
  endfunction

  " Clear and redraw screen when entering and exiting quixkfix windows
  autocmd BufWinEnter,BufWinLeave *
    \ if &ft == 'qf' |
    \   exec 'redraw!' |
    \ endif

endif

if has("gui_gtk")

  " always show tabline on laptop screen
  if system("/usr/bin/xrandr | awk -F',' '/Screen 0/ {gsub(\"( |current)\",\"\");printf $2}'") == "1024x768"
    set showtabline=2
  endif

endif
