call pathogen#infect()

set nocompatible                  " Disable vi compatibility (must come first)

set encoding=utf-8                " Force utf8
set nobomb                        " but don't bother with a BOM

set nomodeline                    " No modelines (potential for security issues)

set cursorline                    " Highlight the cursor line

set ttyfast                       " Assume a fast terminal connection

runtime macros/matchit.vim        " Load the matchit plugin

syntax enable                     " Turn on syntax highlighting
filetype plugin indent on         " Turn on file type detection and indentation

set autoindent                    " Auto-indent if no file type specific indent

set expandtab ts=2 sts=2 sw=2     " Default to two-space indent, no tabs

set showcmd                       " Show incomplete commands as you type
set showmode                      " Show mode when insert, replace or visual

set backspace=indent,eol,start    " Intuitive backspacing

set hidden                        " Allow hidden buffers with unsaved changes

set wildmenu                      " Enhanced command line completion
set wildmode=list:longest         " Complete files like a shell

set ignorecase                    " Ignore case when searching
set smartcase                     " Except when search contains capital letters
if exists("&tagcase")
  set tagcase=followscs           " Update tag search to to the same
endif
set infercase                     " And infer case when doing keyword completion

if exists("&fileignorecase")
  set nofileignorecase            " Make file name completion case-sensitive
endif

set number                        " Show line numbers in left margin
set ruler                         " Show cursor position in default status line

set incsearch                     " Highlight search match as you type
set nohlsearch                    " But clear highlighting once search completed

set nowrap                        " Turn off line wrapping by default
set scrolloff=3                   " Show 3 lines of context around the cursor

set title                         " Set the terminal's title

set visualbell                    " No beeping

set confirm                       " Allow dialog to confirm an action
set autoread                      " Skip confirm for file changed outside vim
set writeany                      " Allow writing of any file without confirm

set writebackup                   " Create backups before overwriting files
set nobackup                      " But don't retain backups after writing
set directory=$HOME/.vim/tmp//    " Keep swap files in one location
set backupdir=$HOME/.vim/tmp//    " And backups in the same place

set laststatus=2                  " Show the status line all the time

set splitright                    " Open vertical splits to the right
set splitbelow                    " Open horizontal splits below

set t_Co=256                      " Assume a 256 color terminal

set background=dark               " Use dark background in terminal vim
if $SSH_CLIENT == ""
   \ && $SUDO_USER == ""
  colorscheme solarized           " With solarized colour scheme locally
else
  colorscheme jellybeans          " And jellybeans when running remotely
endif

if has('balloon_eval')
  set noballooneval               " Disable slow and annoying tooltips
endif

if exists("&synmaxcol")
  set synmaxcol=200               " Disable syntax highlighting on very long lines
endif

set nofoldenable                  " Open all folds by default
set foldlevel=1                   " Fold only one level deep by default
set foldlevelstart=1              " Reset the fold level when opening new files

" Suffixes that can be ignored when completing file names
set suffixes=.bak,~,.swp,.o,.info,.aux,.log,.dvi,.bbl,.blg,.brf,.cb,.ind,.idx,.ilg,.inx,.out,.toc

" Patterns that should be ignored when expanding wildcards
set wildignore=*~,*.o,*.log,*.aux,*.dvi,*.pdf,*.gif,*.jpg,*.png,*.pyc

" Custom status line, show relative file path and file type
set statusline=%<%.99f\ %h%w%m%r\ %=\ %{&ft}\ %l\:\%-c\ %P

" store tabs, windows, buffers etc. in sessions
set sessionoptions=blank,buffers,curdir,tabpages,winsize,winpos,resize

" save globals with viminfo - sessionman stores last session info in global
set viminfo='100,<50,s10,h,!

" set list mode characters used to display whitespace
set listchars=tab:→·,trail:·,eol:¶

" Ins mode completion options - complete without menu if just one match,
" only complete to longest common match, don't use preview scratch window
set completeopt=menu,longest

" Use Space to close completion menu and go back to original text
" I'd prefer to use Esc, but things got weird when I tried Esc
inoremap <expr> <Space> pumvisible() ? '<C-e>' : '<Space>'

" don't automatically save session on exit in non-gui vim
let sessionman_save_on_exit=0

" show private gists when listing mine
let g:gist_show_privates = 1

" Make sure tagbar uses exuberant ctags
if filereadable('/usr/local/bin/ctags')
  let g:tagbar_ctags_bin = '/usr/local/bin/ctags'
endif

" Workaround for tagbar setting ballooneval on open, but never unsetting
if has('balloon_eval')
  autocmd WinEnter __Tagbar__ set ballooneval
  autocmd WinLeave __Tagbar__ set noballooneval
endif

" Show relative paths by default in bufexplorer
let g:bufExplorerShowRelativePath = 1

" Show no name buffers by default in bufexplorer
let g:bufExplorerShowNoName = 1

" Disable default BufExplorer key mappings (use <leader>b to open BufExplorer)
let g:bufExplorerDisableDefaultKeyMapping=1

" Let supertab guess the completion type from current context
let g:SuperTabDefaultCompletionType = "context"
" Use keyword completion if the context can't be determined
let g:SuperTabContextDefaultCompletionType = "<c-n>"
" Fall back to keyword completion when omni completion fails
autocmd FileType *
  \ if &omnifunc != '' |
  \   call SuperTabChain(&omnifunc, "<c-n>") |
  \ endif
" Use opening and closing tags to trigger html omnicompletion
autocmd FileType html,xhtml
  \ let b:SuperTabContextTextMemberPatterns = ['<', '</']

" Don't automagically colorize text, use <leader>tc to toggle
let g:colorizer_startup = 0

" Don't preserve missing EOL by default
" Enable per buffer with `let b:PreserveNoEOL = 1`
let g:PreserveNoEOL = 0

" Disable delimitMate within comments and strings (default is comments)
let delimitMate_excluded_regions = "Comment,String"

" Ignore some additional files to make CtrlP load faster
let g:ctrlp_custom_ignore = {
  \ 'dir': '\v[\/](tmp|log|vendor|coverage|images|fonts|node_modules)$'
  \ }

" Use the current vim working directory as the CtrlP working directory
let g:ctrlp_working_path_mode = 0

" Customise dimensions of CtrlP match window (show more than 10 results)
let g:ctrlp_match_window = 'bottom,order:btt,min:20,max:20,results:25'

" Include the current file in the list of matches
let g:ctrlp_match_current_file = 1

" Let CtrlP reuse nofile buffers when opening files (e.g. BufExplorer)
" FIXME: This doesn't seem to make a blind bit of difference anymore,
" NERDTree uses a nofile buffer, and CtrlP opens files in that buffer
let g:ctrlp_reuse_window = 'netrw\|nofile'

" Prevent CtrlP opening files inside non-writeable buffers, e.g. NERDTree
" See https://vi.stackexchange.com/questions/10016/stop-ctrlp-from-opening-in-nerdtree
function! CtrlPCommand(cmd)
  let c = 0
  let wincount = winnr('$')
  " Don't open it here if current buffer is not writable (e.g. NERDTree)
  while !empty(getbufvar(+expand("<abuf>"), "&buftype")) && c < wincount
    exec 'wincmd w'
    let c = c + 1
  endwhile
  exec a:cmd
endfunction
let g:ctrlp_map = ''
nnoremap <C-p> :call CtrlPCommand('CtrlPMixed')<CR>

" Tell NERDTree to respect wildignore like CtrlP does
let NERDTreeRespectWildIgnore = 1

" Workaround for NerdTreeToggle failures when NerdTree buffer deleted
" See https://github.com/scrooloose/nerdtree/issues/162#issuecomment-107643011
function! NERDTreeCommand()
  try | NERDTreeToggle | catch | NERDTree | endtry
endfunction

" Skip editorconfig with fugitive
let g:EditorConfig_exclude_patterns = ['fugitive://.*']

" Prevent editorconfig from enabling automatic text wrapping
let g:EditorConfig_preserve_formatoptions = 1

" Disable editorconfig use of colorcolumn to highlight long lines
" See custom autocmd, which subtly highlights long lines instead
let g:EditorConfig_max_line_indicator = 'none'

" Don't autohide scratch window when leaving insert mode
let g:scratch_insert_autohide = 0

" map leader to comma so it's in the same position on all keyboards
let mapleader = ","

" Delete key sends text to blackhole register
nnoremap <Del> "_x
vnoremap <Del> "_x

" Make Y act like D and C, i.e. yank until EOL rather than yank entire line
noremap Y y$

" Stop opening the command line history by accident
nnoremap q: :q

" Stop recording commands into registers by accident
nnoremap q :q

" Retain selection after shifting highlighted lines in visual mode
vnoremap > >gv
vnoremap < <gv

" Allow standard windows & gnome shortcuts to shift lines (for pairing)
vnoremap <Tab> >
vnoremap <S-Tab> <

" Tab navigation with Ctrl-Tab (only works in gui vim)
noremap <C-Tab> :tabnext<cr>
noremap <S-C-Tab> :tabprevious<cr>

" Fold mappings (normal mode only)
" <space> toggles current fold
nnoremap <Space> za
" zz opens all folds
nnoremap zz zR
" Z closes all folds
nnoremap Z zM

" Open alternate file (simpler than CTRL-^ or CTRL-6)
nnoremap <Leader>a <C-^>

" Window mappings
" open alternate file in new vertial split
nnoremap <leader>wa :wincmd v<CR><C-^>
" old mapping to open alt file in split
nnoremap <leader>w :wincmd v<CR><C-^>
" shortcuts for opening new splits
nnoremap <leader>ws :wincmd s<CR>
nnoremap <leader>wv :wincmd v<CR>
" rotate and exchange windows
nnoremap <leader>wr :wincmd r<CR>
nnoremap <leader>wx :wincmd x<CR>
" navigation with arrow keys
nnoremap <C-Up> :wincmd k<CR>
nnoremap <C-Down> :wincmd j<CR>
nnoremap <C-Left> :wincmd h<CR>
nnoremap <C-Right> :wincmd l<CR>
" resize all windows equally
nnoremap = :wincmd =<CR>
" resize dimensions of current window
nnoremap + :wincmd +<CR>
nnoremap - :wincmd -<CR>
nnoremap > :wincmd ><CR>
nnoremap < :wincmd <<CR>

" Tag navigation
" remap CTRL-] to show list when multiple matches (use tjump)
nnoremap <C-]> g<C-]>
" use CTRL-\ to jump to definition in next available window
function! JumpToTagInSplit()
  let currword = expand("<cword>")
  let startwin = winnr()
  " Move to next window with a normal buffer
  let c = 0
  let n = winnr('$')
  while c < n
    exec 'wincmd w'
    if empty(getbufvar(+expand("<abuf>"), "&buftype"))
      break
    endif
    let c = c + 1
  endwhile
  " Back where we started? Open a new split
  if winnr() == startwin
    exec 'wincmd v'
  endif
  try
    exec "tjump ".currword
  catch
    " Go back to startwin on error
    exec startwin . "wincmd w"
    echoerr v:exception
  endtry
endfunction
nnoremap <C-\> :call JumpToTagInSplit()<CR>

" File explorer / tree view
nnoremap <leader>e :call NERDTreeCommand()<CR>

" Fuzzy find files
nnoremap <leader>f :call CtrlPCommand('CtrlP')<CR>

" Fuzzy find open buffers
nnoremap <leader>b :call CtrlPCommand('CtrlPBuffer')<CR>

" Fuzzy find MRU files
nnoremap <leader>m :call CtrlPCommand('CtrlPMRU')<CR>

" Fuzzy find tags
nnoremap <leader>t :call CtrlPCommand('CtrlPTag')<CR>

" Search in files
nnoremap <Leader>s :Ack!<Space>

" List projects
nnoremap <leader>p :SessionSave<CR>:SessionList<CR>

" List open files/buffers
nnoremap ; :ToggleBufExplorer<CR>

" Outline view of current file
nnoremap <leader>o :TagbarToggle<Enter>

" Zoom in/out current window
nnoremap <leader>x :ZoomWin<Enter>

" Highlight whitespace
nnoremap <leader><space> :ToggleSpaceHi<Enter>

" Run ctags to (re)generate tags file
nnoremap <Leader>ct :!ctags -R .<CR>

" Toggle spell checking
inoremap <Leader>; <C-o>:setlocal spell! spelllang=en_gb<CR>
nnoremap <Leader>; :setlocal spell! spelllang=en_gb<CR>

" Remap gs to open Scratch window in normal rather than insert mode
let g:scratch_no_mappings = 1 " must disable plugin mappings first
nnoremap gs :Scratch<CR>

" Remove current search highlighting when redrawing screen
nnoremap <C-L> :nohl<CR>:redraw!<CR>

if has('mouse') && has("clipboard")
  set mouse=a

  " yank to and paste from system clipboard if possible
  if has("x11") && ( v:version >= 704 || v:version == 703 && has("patch074") )
    set clipboard=unnamedplus
  else
    set clipboard=unnamed
  endif

  " Use windows keyboard shortcuts (taken from mswin.vim)
  " CTRL-X and SHIFT-Del are Cut
  vnoremap <C-X> "+x
  vnoremap <S-Del> "+x
  " CTRL-C and CTRL-Insert are Copy
  vnoremap <C-C> "+y
  vnoremap <C-Insert> "+y
  " CTRL-V and SHIFT-Insert are Paste
  nnoremap <C-V> "+gP
  nnoremap <S-Insert> "+gP
  cnoremap <C-V> <C-R>+
  cnoremap <S-Insert> <C-R>+
  exe 'inoremap <script> <C-V>' paste#paste_cmd['i']
  exe 'vnoremap <script> <C-V>' paste#paste_cmd['v']

  " Make the mouse/trackpad work in terminal
  " See https://wincent.com/blog/tweaking-command-t-and-vim-for-use-in-the-terminal-and-tmux
  if &term =~ "xterm" || &term =~ "screen"
    " as of March 2013, this works:
    set ttymouse=xterm2

    " previously, I found that ttymouse was getting reset, so had
    " to reapply it via an autocmd like this:
    autocmd VimEnter,FocusGained,BufEnter * set ttymouse=xterm2
  endif
endif

" Don't wrap existing long lines in insert mode
autocmd FileType * setlocal formatoptions+=l

" Disable automatic text wrapping by default
autocmd FileType * setlocal formatoptions-=t

" Continue comments when hitting enter in insert mode
autocmd FileType * setlocal formatoptions+=r

" Continue comments when hitting o or O in normal mode
autocmd FileType * setlocal formatoptions+=o

" Additional ruby syntax highlighting
autocmd BufRead,BufNewFile {Berksfile,Gemfile,Rakefile,Capfile,Vagrantfile,Thorfile,pryrc,config.ru}
  \ set ft=ruby |

" Set conf.local filetype to conf
autocmd BufRead,BufNewFile *.conf.local set ft=conf

" Assume *.template files are json (cloudformation templates)
autocmd BufNewFile,BufRead *.template set filetype=json

" Turn on spell checking by default for git commits
autocmd FileType gitcommit setlocal spell spelllang=en_gb

" Re-enable automatic text wrapping for git commits
autocmd FileType gitcommit setlocal formatoptions+=t

" Treat annotated git tags as git commits
autocmd BufNewFile,BufRead TAG_EDITMSG set filetype=gitcommit

" Automatically wrap markdown text at 80 characters
autocmd FileType markdown setlocal textwidth=80
autocmd FileType markdown setlocal formatoptions+=t

" Use html omnicompletion for xhtml documents
autocmd FileType xhtml set omnifunc=htmlcomplete#CompleteTags

" Use syntax based completion when no omnicompletion available
if has("autocmd") && exists("+omnifunc")
  autocmd Filetype *
    \	if &omnifunc == "" |
    \	  setlocal omnifunc=syntaxcomplete#Complete |
    \	endif
  endif

" Vimcast 4 - Tidying Whitespace
" Function and autocmd to strip trailing whitespace from files when saving
function! <SID>StripTrailingWhitespace()
  " Preparation: save last search, and cursor position.
  let _s=@/
  let l = line(".")
  let c = col(".")
  " Do the business:
  %s/\s\+$//e
  " Clean up: restore previous search history, and cursor position
  let @/=_s
  call cursor(l, c)
endfunction
" enable whitespace stripping for known file types by default
autocmd FileType * let b:strip_trailing_whitespace=1
" disable whitespace stripping for other file types as needed
autocmd FileType markdown let b:strip_trailing_whitespace=0
" enable whitespace stripping globally by default
let g:strip_trailing_whitespace=1
" strip whitespace before writing if enabled globally and for buffer
autocmd BufWritePre *
  \ if ( exists('g:strip_trailing_whitespace') && g:strip_trailing_whitespace )
  \ && ( exists('b:strip_trailing_whitespace') && b:strip_trailing_whitespace ) |
  \   call <SID>StripTrailingWhitespace() |
  \ endif

" Subtly highlight long lines rather than display colorcolumn
autocmd BufEnter *
  \ if &textwidth > 0 |
  \   call matchadd('ColorColumn', '\%>'.&tw.'v.\+', -1) |
  \ elseif &ft != 'qf' && &ft != '' |
  \   call matchadd('ColorColumn', '\%>80v.\+', -1) |
  \ endif

" Jump to last known cursor position when editing (except for git commits)
autocmd BufReadPost *
  \ if line("'\"") > 1 && line("'\"") <= line("$") && &ft != 'gitcommit' |
  \   exe "normal! g`\"" |
  \ endif

" Diff a buffer with the file from which it was originally loaded
if !exists(":DiffOrig")
  command DiffOrig vert new | set bt=nofile | r ++edit # | 0d_ | diffthis
      \ | wincmd p | diffthis
endif

" Set working directory when opening vim to edit a particular file
function! <SID>SetWorkingDir()
  if argc() != 0
    if isdirectory(argv(0))
      exec 'cd' argv(0)
    elseif filereadable(argv(0))
      exec 'cd %:p:h'
    end
  endif
endfunction
autocmd VimEnter * call <SID>SetWorkingDir()

" Close NERDTree before leaving vim, because it doesn't reopen correctly
" FIXME: prob should do this with all nofile, help and quickfix windows
autocmd VimLeavePre * NERDTreeClose
