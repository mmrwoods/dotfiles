call pathogen#infect()

set nocompatible                  " Disable vi compatibility (must come first)

set encoding=utf-8                " Default to UTF8, don't use value from $LANG
set nobomb                        " And don't add a BOM, assume UTF8 everywhere

set nomodeline                    " No modelines (potential for security issues)

set cursorline                    " Highlight the cursor line

set ttyfast                       " Assume a fast terminal connection

runtime macros/matchit.vim        " Load the matchit plugin

syntax enable                     " Turn on syntax highlighting
filetype plugin indent on         " Turn on file type detection and indentation

set autoindent                    " Auto-indent if no file type specific indent

set expandtab sts=2 sw=2          " Default to two-space indent, no tabs

set showcmd                       " Show incomplete commands as you type

set backspace=indent,eol,start    " Intuitive backspacing

set hidden                        " Allow hidden buffers with unsaved changes

set wildmenu                      " Enhanced command line completion
set wildmode=list:longest         " Complete files like a shell

set ignorecase                    " Ignore case when searching
set smartcase                     " Except when search contains capital letters
if exists("&tagcase")
  set tagcase=followscs           " Update tag search to to the same
endif
set infercase                     " And infer case when doing keyword completion

if exists("&fileignorecase")
  set nofileignorecase            " Make file name completion case-sensitive
endif

set number                        " Show line numbers in left margin
set ruler                         " Show cursor position in default status line

set incsearch                     " Highlight search match as you type
set nohlsearch                    " But clear highlighting once search completed

set nowrap                        " Turn off line wrapping by default
set scrolloff=3                   " Show 3 lines of context around the cursor

set title                         " Set the terminal's title

set visualbell                    " No beeping

set confirm                       " Allow dialog to confirm an action
set autoread                      " Skip confirm for file changed outside vim
set writeany                      " Allow writing of any file without confirm

set writebackup                   " Create backups before overwriting files
set nobackup                      " But don't retain backups after writing
set directory=$HOME/.vim/tmp//    " Keep swap files in one location
set backupdir=$HOME/.vim/tmp//    " And backups in the same place

set laststatus=2                  " Show the status line all the time

set splitright                    " Open vertical splits to the right
set splitbelow                    " Open horizontal splits below

set t_Co=256                      " Assume a 256 color terminal, ignore $TERM

if $TERM_PROGRAM =~# 'iTerm'
  \ || has('gui_running')
  colorscheme solarized           " Solarized colors for local development
elseif $SSH_CLIENT != ''
  colorscheme jellybeans          " And jellybeans when running remotely
endif

if has('balloon_eval')
  set noballooneval               " Disable slow and annoying tooltips
endif

if exists("&synmaxcol")
  set synmaxcol=200               " Disable syntax highlighting on very long lines
endif

set nofoldenable                  " Open all folds by default
set foldlevel=99                  " Don't open/close all folds at same indent
set foldlevelstart=99             " Reset fold level when opening new files
set foldmethod=indent             " Keep it simple stupid, just fold on indent
set foldignore=''                 " Don't ignore # when folding on indent

" Suffixes that can be ignored when completing file names
set suffixes=.bak,~,.swp,.o,.info,.aux,.log,.dvi,.bbl,.blg,.brf,.cb,.ind,.idx,.ilg,.inx,.out,.toc

" Patterns that should be ignored when expanding wildcards
set wildignore=*~,*.o,*.log,*.aux,*.dvi,*.pdf,*.gif,*.jpg,*.png,*.pyc,.DS_Store,*.egg*,.git,__pycache__

" Custom status line, show relative file path and file type
set statusline=%<%.99f\ %h%w%m%r\ %=\ %{&ft}\ %l\:\%-c\ %P

" Store windows, tabs, current directory, and buffer info in sessions
" Exclude blank to avoid issues with nofile & quickfix windows (e.g.
" NERDTree, Tagbar, Ack), which are not well supported by vim sessions
set sessionoptions=buffers,curdir,tabpages,winsize,winpos,resize

" save globals with viminfo - sessionman stores last session info in global
set viminfo='100,<50,s10,h,!

" set list mode characters used to display unwanted whitespace
set listchars=tab:│→,nbsp:∘,trail:·

" Ins mode completion options - complete without menu if just one match,
" only complete to longest common match, don't use preview scratch window
set completeopt=menuone,longest

" Include dictionary words in completion menu when spellcheck is enabled
set complete+=kspell

" When joining lines, use one space rather than two after punctuation
set nojoinspaces

" If using grep, at least ignore binaries and exclude some paths
set grepprg="grep -n -I --exclude=tags --exclude-dir=.git $* /dev/null"

" don't automatically save session on exit in non-gui vim
let sessionman_save_on_exit=0

" show private gists when listing mine
let g:gist_show_privates = 1

" Don't add the recent files menu from MRU plugin
let MRU_Add_Menu = 0

" Use the current window to load the list of MRU files
let MRU_Use_Current_Window = 1

" Don't add git commits etc. to MRU files
let MRU_Exclude_Files = '\.git*'

" Make sure tagbar uses exuberant ctags
if filereadable('/usr/local/bin/ctags')
  let g:tagbar_ctags_bin = '/usr/local/bin/ctags'
endif

" Workaround for tagbar setting ballooneval globally on open/toggle
if has('balloon_eval')
  autocmd WinEnter __Tagbar__* set ballooneval
  autocmd WinLeave __Tagbar__* set noballooneval
endif

" Assume a POSIX-compatible shell when shebang specifies /bin/sh, otherwise
" highlighting is system dependent as the sh syntax file resolves symlinks
let g:is_posix = 1

" Tell netrw to also stop enabling ballooneval
let g:netrw_nobeval = 1

" Show relative paths by default in bufexplorer
let g:bufExplorerShowRelativePath = 1

" Show no name buffers in bufexplorer, I want to be able to find them easily
" Note: BufExplorer lists quickfix and location list buffers as [No Name] if
" it finds them in the buffer list. This is confusing and makes it easy to
" break things, so I'm using the vim-qf plugin to hide them from the list.
" FIXME: this probably warrants forking and fixing BufExplorer to hide them.
let g:bufExplorerShowNoName = 1

" Disable default BufExplorer key mappings (use custom mappings)
let g:bufExplorerDisableDefaultKeyMapping = 1

" Let supertab guess the completion type from current context
let g:SuperTabDefaultCompletionType = "context"

" Use keyword completion if the context can't be determined
let g:SuperTabContextDefaultCompletionType = "<c-n>"

" Always try to match case for supertab completions
let g:SuperTabCompleteCase = 'match'

" Reset supertab completion type once a completion has been chosen
let g:SuperTabRetainCompletionDuration = 'completion'

" Fall back to keyword completion when omni completion fails
autocmd FileType *
  \ if &omnifunc != '' |
  \   call SuperTabChain(&omnifunc, "<c-n>") |
  \ endif

" Use opening and closing tags to trigger html omnicompletion
autocmd FileType html,xhtml
  \ let b:SuperTabContextTextMemberPatterns = ['<', '</']

" Don't automagically colorize text, use <leader>tc to toggle
let g:colorizer_startup = 0

" Don't preserve missing EOL by default
" Enable per buffer with `let b:PreserveNoEOL = 1`
let g:PreserveNoEOL = 0

" Disable delimitMate within comments and strings (default is comments)
let delimitMate_excluded_regions = "Comment,String"

" Tell NERDTree to respect wildignore
let NERDTreeRespectWildIgnore = 1

" Stop NERDTree from hijacking netrw, I want to use both :-)
let NERDTreeHijackNetrw = 0

" Show hidden files in NERDTree (same as netrw)
let NERDTreeShowHidden = 1

" Custom statusline for NERDTree (just show buffer name, e.g. NERD_tree_1)
" Another option to consider is to show the currently selected file, e.g.
" https://github.com/vim-airline/vim-airline/issues/271#issuecomment-40597276
let NERDTreeStatusline = '%t'

" Always show the netrw banner, so it's not mistaken for NERDTree
let g:netrw_banner = 1

" Don't go back to netrw when choosing to edit the alternate file,
" actually go back to the last edited file (same as bufexplorer)
let g:netrw_altfile = 1

" Workaround for NerdTreeToggle failures when NerdTree buffer deleted
" See https://github.com/scrooloose/nerdtree/issues/162#issuecomment-107643011
function! NERDTreeCommand()
  try | NERDTreeToggle | catch | NERDTree | endtry
endfunction

" Skip editorconfig with fugitive
let g:EditorConfig_exclude_patterns = ['fugitive://.*']

" Prevent editorconfig from enabling automatic text wrapping
let g:EditorConfig_preserve_formatoptions = 1

" Disable editorconfig use of colorcolumn to highlight long lines
" See custom autocmd, which subtly highlights long lines instead
let g:EditorConfig_max_line_indicator = 'none'

" Don't autohide scratch window when leaving insert mode
let g:scratch_insert_autohide = 0

" Highlight JSX embedded in javascript files
let g:jsx_ext_required = 0

" Don't set up any gitgutter key mappings (I only use it to show the diff)
let g:gitgutter_map_keys = 0

" Disable gitgutter by default, enable as needed using gd mapping (see below)
let g:gitgutter_enabled = 0

" Disable gd mapping for GoDef command, gd = git diff, use CTRL-[ for GoDef
let g:go_def_mapping_enabled = 0

" Don't automatgically resize the quickfix window if number items lt 10
let g:qf_auto_resize = 0

" Prefix fzf commands with FZF, e.g. Files becomes FZFFiles (avoids polluting
" top level namespace and makes it easier to find and complete FZF commands)
let g:fzf_command_prefix = 'FZF'

" map leader to comma so it's in the same position on all keyboards
let mapleader = ","

" Use ag command (aka The Silver Searcher) for finding and listing files
if executable('ag')
  " Use ag over grep
  set grepprg=ag\ --vimgrep
  set grepformat=%f:%l:%c:%m

  " use ag with ack.vim
  let g:ackprg = 'ag --vimgrep'

  " Use ag in fzf for listing files. Lightning fast and respects .gitignore
  let $FZF_DEFAULT_COMMAND = 'ag --literal --files-with-matches --nocolor --hidden -g ""'
endif

" Set bat theme for fancy syntax highlighting in FZF preview window
if executable('bat')
  " GitHub theme works well with dark and light backgrounds and color schemes
  let $BAT_THEME='GitHub'
endif

" FZF color scheme, see https://github.com/junegunn/fzf/wiki/Color-schemes
function! <SID>UpdateFZFColors()
  let colors_name = get(g:, 'colors_name', 'default')
  if colors_name ==# 'solarized'
    if &background ==# 'light'
      let $FZF_DEFAULT_OPTS='
        \ --color fg:240,bg:230,hl:33,fg+:241,bg+:221,hl+:33
        \ --color info:33,prompt:33,pointer:166,marker:166,spinner:33'
    else
      let $FZF_DEFAULT_OPTS='
        \ --color dark,hl:33,hl+:37,fg+:235,bg+:136,fg+:254
        \ --color info:254,prompt:37,spinner:108,pointer:235,marker:235'
    endif
  elseif ( colors_name ==# 'jellybeans' )
    let $FZF_DEFAULT_OPTS='
      \ --color fg:188,bg:233,hl:103,fg+:222,bg+:234,hl+:104
      \ --color info:183,prompt:110,spinner:107,pointer:167,marker:215'
  else
    let $FZF_DEFAULT_OPTS='--color ' . &background
  endif
endfunction
autocmd VimEnter,ColorScheme * call <SID>UpdateFZFColors()

" Add M mapping to use easyclip function to move until EOL
nmap M <Plug>MoveMotionEndOfLinePlug

" Stop opening the command line history by accident
nnoremap q: :q

" Stop recording commands into registers by accident
nnoremap q :q

" Retain selection after shifting highlighted lines in visual mode
vnoremap > >gv
vnoremap < <gv

" Make backspace work with visual selections again. This used to work by
" default in version 8.1.1722, but upgrading to version 8.1.2234 broke it,
" without this mapping, backspacing in visual mode just moves the cursor.
vnoremap <BS> "_x

" Move block selections around in visual mode with schlepp plugin
vmap <C-Up> <Plug>SchleppUp
vmap <C-Down> <Plug>SchleppDown
vmap <C-Left> <Plug>SchleppLeft
vmap <C-Right> <Plug>SchleppRight

" Allow standard windows & gnome shortcuts to shift lines (for pairing)
vnoremap <Tab> >
vnoremap <S-Tab> <

" Tab navigation with Ctrl-Tab (only works in gui vim)
noremap <C-Tab> :tabnext<cr>
noremap <S-C-Tab> :tabprevious<cr>

" Fold mappings (normal mode only)
" <space> toggles current fold
nnoremap <Space> za
" zz opens all folds
nnoremap zz zR
" Z closes all folds
nnoremap Z zM

" Open the alternate file, i.e. previously edited file in the current window
nnoremap <Leader>g <C-^>

" Open plugin defined alternate file, e.g. unit test file for model
" This relies on plugins like rails.vim that follow the conventions of a.vim
" and create an A command to find the file and open it in the current window
nnoremap <Leader>a :A<CR>

" Window mappings
" switch to window pseudo-mode (uses window mode plugin, no more keyboard
" contortions - window mode automatically prefixes <C-w> to all keypresses)
nnoremap <leader>wm :WindowMode<CR>
" open vim defined alternate file in new vertial split
nnoremap <leader>wg :vs#<CR>
" open plugin defined alternate file in new vertial split
nnoremap <leader>wa :AV<CR>
" shortcuts for opening new splits
nnoremap <leader>ws :wincmd s<CR>
nnoremap <leader>wv :wincmd v<CR>
" rotate and exchange windows
nnoremap <leader>wr :wincmd r<CR>
nnoremap <leader>wx :wincmd x<CR>
" navigation with arrow keys
nnoremap <C-Up> :wincmd k<CR>
nnoremap <C-Down> :wincmd j<CR>
nnoremap <C-Left> :wincmd h<CR>
nnoremap <C-Right> :wincmd l<CR>
" resize all windows equally
nnoremap = :wincmd =<CR>
" resize dimensions of current window
nnoremap + :wincmd +<CR>
nnoremap - :wincmd -<CR>
nnoremap > :wincmd ><CR>
nnoremap < :wincmd <<CR>

" Tag navigation
" remap CTRL-] to show list when multiple matches (use tjump)
nnoremap <C-]> g<C-]>
" use CTRL-\ to jump to definition in next available window
function! JumpToTagInSplit()
  let currword = expand("<cword>")
  let startwin = winnr()
  " Move to next window with a normal buffer
  let c = 0
  let n = winnr('$')
  while c < n
    exec 'wincmd w'
    if empty(getbufvar(+expand("<abuf>"), "&buftype"))
      break
    endif
    let c = c + 1
  endwhile
  " Back where we started? Open a new split
  if winnr() == startwin
    exec 'wincmd v'
  endif
  try
    exec "tjump ".currword
  catch
    " Go back to startwin on error
    exec startwin . "wincmd w"
    echoerr v:exception
  endtry
endfunction
nnoremap <C-\> :call JumpToTagInSplit()<CR>

" Toggle project drawer / tree view
nnoremap <leader>d :call NERDTreeCommand()<CR>

" Locate current file in project drawer / tree view
nnoremap <leader>l :NERDTreeFind<CR>

" Show/hide git diff in sign column (uses gitgutter plugin)
nnoremap gd :GitGutterToggle<CR>

" Toggle file explorer (netrw, enhanced with vim-vinegar)
nmap <expr> <Leader>e (&ft == 'netrw' ? ':Rexplore<CR>' : '<Plug>VinegarUp')

" Stop fuzzy finder opening files in nofile and quickfix windows, e.g NERDTree
" FIXME: extract to generic function and/or command and use with Ack mapping
function! FZFCommand(cmd)
  let c = 0
  let wincount = winnr('$')
  " Move to a window containing a re-usable buffer before running the command
  while ( bufname() =~# 'NERD_tree' || !empty(getbufvar(+expand("<abuf>"), "&buftype")) )
        \  && bufname() !~# 'BufExplorer\|MRU_Files' && c < wincount
    exec 'wincmd w'
    let c = c + 1
  endwhile
  exec a:cmd
endfunction

" Fuzzy finder, default mode
nnoremap <leader>f :call FZFCommand('FZFFiles')<CR>

" Fuzzy find files
nnoremap <leader>ff :call FZFCommand('FZFFiles')<CR>

" Fuzzy find buffers
nnoremap <leader>fb :call FZFCommand('FZFBuffers')<CR>

" Fuzzy find MRU files within current working directory / project
" Note: relies on MRU plugin, use <leader>m to see all MRU files
nnoremap <leader>fm : call fzf#run(fzf#wrap({'source': 'grep ^' . getcwd() . ' ~/.vim_mru_files', 'sink': 'e'}))<CR>

" Fuzzy find tags
nnoremap <leader>ft :call FZFCommand('FZFTags')<CR>

" Search in files using ack.vim, with fallback to git grep
if exists('g:ackprg') || executable('ack') || executable('ack-grep')
  let searchcmd = 'Ack!'
else
  let searchcmd = 'Ggrep!'
endif
nnoremap <expr> <Leader>s (expand('%') =~ 'NERD_tree' ? "\<c-w>\<c-w>" : '') . ":" . searchcmd . "\<Space>"

" Search in files for word under cursor
nnoremap <leader>y "syiw:Ack! <c-r>s<space>

" List projects
nnoremap <leader>p :SessionSave<CR>:SessionList<CR>

" List open files/buffers
nnoremap <leader>b :ToggleBufExplorer<CR>

" List most recently used files
nnoremap <leader>m :MRU<CR>

" Outline view of current file
" Note: tagbar plugin automatically enables ballooneval globally :-(
nnoremap <leader>o :TagbarToggle<Enter>:set noballooneval<CR>

" Zoom in/out current window
nnoremap <leader>z :ZoomWin<Enter>

" Highlight whitespace
nnoremap <leader><space> :ToggleSpaceHi<Enter>

" Run ctags to (re)generate tags file
nnoremap <Leader>ct :!ctags -R .<CR>

" Toggle spell checking
nnoremap <Leader>ts :setlocal spell!<CR>

" Toggle list mode (show unwanted whitespace)
nnoremap <Leader>tl :setlocal list!<CR>

" Some extra mappings for convenience/speed
" Single key to toggle bufexplorer, I use this all the time
nnoremap ; :ToggleBufExplorer<CR>

" Remap gs to open Scratch window in normal rather than insert mode
let g:scratch_no_mappings = 1 " must disable plugin mappings first
nnoremap gs :Scratch<CR>

" Remove current search highlighting when redrawing screen
nnoremap <C-L> :nohl<CR>:redraw!<CR>

if has("clipboard")
  " copy to and paste from system clipboard
  set clipboard=unnamed

  if has("unnamedplus")
    " and to X11 primary selection if available
    set clipboard+=unnamedplus
  endif

  " Allow basic windows/gnome/kde editing shortcuts. Copied from mswin.vim to
  " help with pairing, but implements fewer shortcuts, and in fewer modes, so
  " this doesn't break things like the CTRL-V mapping for visual block mode.

  " CTRL-X and SHIFT-Del are Cut in visual mode
  vnoremap <C-X> "+x
  vnoremap <S-Del> "+x
  " CTRL-C and CTRL-Insert are Copy in visual mode
  vnoremap <C-C> "+y
  vnoremap <C-Insert> "+y
  " CTRL-V and SHIFT-Insert are Paste in insert and visual modes
  exe 'inoremap <script> <C-V>' paste#paste_cmd['i']
  exe 'vnoremap <script> <C-V>' paste#paste_cmd['v']
  inoremap <S-Insert> <C-V>
  vnoremap <S-Insert> <C-V>
  " CTRL-Z is Undo in normal and insert modes
  noremap <C-Z> u
  inoremap <C-Z> <C-O>u
  " CTRL-Y is Redo in normal and insert modes
  noremap <C-Y> <C-R>
  inoremap <C-Y> <C-O><C-R>
  " CTRL-A is Select All in normal, insert and visual modes
  noremap <C-A> gggH<C-O>G
  inoremap <C-A> <C-O>gg<C-O>gH<C-O>G
  vnoremap <C-A> <C-C>ggVG
endif

if has('mouse')
  " enable mouse support in all modes
  set mouse=a

  " Make the mouse/trackpad work with tmux and screen
  " See https://wincent.com/blog/tweaking-command-t-and-vim-for-use-in-the-terminal-and-tmux
  if &term =~ "xterm" || &term =~ "screen"
    " as of March 2013, this works:
    set ttymouse=xterm2

    " previously, I found that ttymouse was getting reset, so had
    " to reapply it via an autocmd like this:
    autocmd VimEnter,FocusGained,BufEnter * set ttymouse=xterm2
  endif
endif

" Default format options, auto-format comments, but not code/text
set formatoptions=croql

" Additional ruby syntax highlighting
autocmd BufRead,BufNewFile {Berksfile,Gemfile,Rakefile,Capfile,Vagrantfile,Thorfile,pryrc,config.ru}
  \ set ft=ruby |

" Set conf.local filetype to conf
autocmd BufRead,BufNewFile *.conf.local set ft=conf

" Assume *.template files are json (cloudformation templates)
autocmd BufNewFile,BufRead *.template set filetype=json

" Turn on spell checking by default for git commits
autocmd FileType gitcommit setlocal spell

" Enable syntax highlighting for long lines in git commits
autocmd FileType gitcommit setlocal synmaxcol=0

" Automatically wrap markdown text at 80 characters
autocmd FileType markdown setlocal textwidth=80
autocmd FileType markdown setlocal formatoptions+=t

" Turn on spell checking by default for markdown files
autocmd FileType markdown setlocal spell

" Use html omnicompletion for xhtml documents
autocmd FileType xhtml set omnifunc=htmlcomplete#CompleteTags

" Use xmllint as exernal program for formatting xml
autocmd FileType xml setlocal equalprg=xmllint\ --format\ --recover\ -\ 2>/dev/null

" Avoid error editing crontab: temp file must be edited in place
autocmd filetype crontab setlocal nobackup nowritebackup

" Use syntax based completion when no omnicompletion available
if exists("+omnifunc")
  autocmd Filetype *
    \	if &omnifunc == "" |
    \	  setlocal omnifunc=syntaxcomplete#Complete |
    \	endif
endif

" Vimcast 4 - Tidying Whitespace
" Function and autocmd to strip trailing whitespace from files when saving
function! <SID>StripTrailingWhitespace()
  " Preparation: save last search, and cursor position.
  let _s=@/
  let l = line(".")
  let c = col(".")
  " Do the business:
  %s/\s\+$//e
  " Clean up: restore previous search history, and cursor position
  let @/=_s
  call cursor(l, c)
endfunction
" enable whitespace stripping for known file types by default
autocmd FileType * let b:strip_trailing_whitespace=1
" disable whitespace stripping for other file types as needed
autocmd FileType markdown let b:strip_trailing_whitespace=0
" enable whitespace stripping globally by default
let g:strip_trailing_whitespace=1
" strip whitespace before writing if enabled globally and for buffer
autocmd BufWritePre *
  \ if ( exists('g:strip_trailing_whitespace') && g:strip_trailing_whitespace )
  \ && ( exists('b:strip_trailing_whitespace') && b:strip_trailing_whitespace ) |
  \   call <SID>StripTrailingWhitespace() |
  \ endif

" Subtly highlight long lines rather than display colorcolumn
function! <SID>HighlightLongLines()
  for l:match in getmatches()
    if get(l:match, 'group', '') == 'ColorColumn'
      call matchdelete(get(l:match, 'id'))
    endif
  endfor
  if &textwidth > 0
    call matchadd('ColorColumn', '\%'.(&tw+1).'v.\+', -1)
  elseif &ft != 'qf' && &ft != ''
    call matchadd('ColorColumn', '\%81v.\+', -1)
  endif
endfunction
" Apply long line highlighting after vim has loaded to allow for plugins
" changing textwidth (notably editorconfig, but others could too I guess)
function! <SID>AutoHighlightLongLines()
  bufdo call <SID>HighlightLongLines()
  autocmd BufNewFile,BufReadPost,BufFilePost * call <SID>HighlightLongLines()
endfunction
autocmd VimEnter * call <SID>AutoHighlightLongLines()

" Jump to last known cursor position when editing (except for git commits)
autocmd BufReadPost *
  \ if line("'\"") > 1 && line("'\"") <= line("$") && &ft != 'gitcommit' |
  \   exe "normal! g`\"" |
  \ endif

" Diff a buffer with the file from which it was originally loaded
if !exists(":DiffOrig")
  command DiffOrig vert new | set bt=nofile | r ++edit # | 0d_ | diffthis
      \ | wincmd p | diffthis
endif

" Set working directory when opening vim to edit a particular file
function! <SID>SetWorkingDir()
  if argc() != 0
    if isdirectory(argv(0))
      exec 'cd' argv(0)
    elseif filereadable(argv(0))
      exec 'cd %:p:h'
    end
  endif
endfunction
autocmd VimEnter * call <SID>SetWorkingDir()
